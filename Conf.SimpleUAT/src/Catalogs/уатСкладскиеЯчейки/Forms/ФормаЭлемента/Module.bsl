
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура SUAT_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	SUAT_ИнициализироватьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура SUAT_ИнициализироватьЭлементыФормы()
	
	ИмяГруппыФормы_SimpleUAT = "ГруппаSimpleUAT";
	ГруппаФормы_SimpleUAT = Элементы.Найти(ИмяГруппыФормы_SimpleUAT);
	Если ГруппаФормы_SimpleUAT = Неопределено Тогда
		
		ГруппаФормы_SimpleUAT = Элементы.Добавить(ИмяГруппыФормы_SimpleUAT, Тип("ГруппаФормы"), ЭтотОбъект);
		ГруппаФормы_SimpleUAT.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаФормы_SimpleUAT.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаФормы_SimpleUAT.ОтображатьЗаголовок = Ложь; 
		ГруппаФормы_SimpleUAT.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
	КонецЕсли;
	
	ИмяРеквизита_ГабаритностьМатериала = "SUAT_ГабаритностьМатериала";
	ЭлементФормы_ГабаритностьМатериала = Элементы.Найти(ИмяРеквизита_ГабаритностьМатериала);
	Если ЭлементФормы_ГабаритностьМатериала = Неопределено Тогда
		
		ЭлементФормы_ГабаритностьМатериала = Элементы.Добавить(
			ИмяРеквизита_ГабаритностьМатериала, Тип("ПолеФормы"), ГруппаФормы_SimpleUAT);
		ЭлементФормы_ГабаритностьМатериала.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементФормы_ГабаритностьМатериала.ПутьКДанным = "Объект." + ИмяРеквизита_ГабаритностьМатериала;
		
	КонецЕсли;
	
	ИмяРеквизита_Заполнено = "SUAT_Заполнено";
	ЭлементФормы_Заполнено = Элементы.Найти(ИмяРеквизита_Заполнено);
	Если ЭлементФормы_Заполнено = Неопределено Тогда
		
		ЭлементФормы_Заполнено = Элементы.Добавить(ИмяРеквизита_Заполнено, Тип("ПолеФормы"), ГруппаФормы_SimpleUAT);
		ЭлементФормы_Заполнено.Вид = ВидПоляФормы.ПолеФлажка;
		ЭлементФормы_Заполнено.ПутьКДанным = "Объект." + ИмяРеквизита_Заполнено;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
